// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // 1 RAM16K for data memory
    // 1 Screen and 1 Keyboard
    DMux(in=load, sel=address[14], a=load0, b=load1); // data or io
    RAM16K(in=in, load=load0, address[0..13]=address[0..13], out=out0);
    DMux(in=load1, sel=address[13], a=load10, b=load11); // screen or keyboard
    Screen(in=in, load=load10, address=address[0..12], out=out10);
    Keyboard(out=out11);
    /** multiplex the right output */
    Mux16(a=out10, b=out11, sel=address[13], out=out1);
    Mux16(a=out0, b=out1, sel=address[14], out=out);
}